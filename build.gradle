/*
 * Copyright (c) 2019-2023 by David Gerber - https://zapek.com
 *
 * This file is part of Xeres.
 *
 * Xeres is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Xeres is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Xeres.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    ext {
        bouncycastleVersion = "1.79"
        apacheCommonsCollectionsVersion = "4.4"
        springOpenApiVersion = "2.7.0"
        jsoupVersion = "1.18.3"
        jsonPatchVersion = "1.13"
        zxingVersion = "3.5.3"
        appDirsVersion = "1.2.2"
        junitVersion = "5.11.4"
        archunitVersion = "1.3.0"
    }
}

plugins {
    id 'org.springframework.boot' version '3.4.0' apply false
    id 'org.flywaydb.flyway' version '10.20.0' apply false // Keep the version in sync with spring-boot from time to time
    id 'org.panteleyev.jpackageplugin' version '1.6.0' apply false
    id 'org.sonarqube' version '6.0.1.5171'
}

// To upgrade Gradle, change the version here, refresh, then run the 'build setup/wrapper' task
wrapper {
    gradleVersion = '8.11.1'
}

// This gives a git-like version for git builds but a proper version
// when the release is built with a tag
def getVersionName = providers.exec {
    commandLine("git", "describe", "--tags")
}.standardOutput.asText.get().substring(1).trim()

subprojects {
    group = 'io.xeres'
    version = "${getVersionName}"

    apply plugin: 'java'
    apply plugin: 'jacoco'

    java {
        sourceCompatibility = '21'
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "zapek_Xeres"
        property "sonar.organization", "zapek"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.cpd.exclusion", "**/*Mapper.java"
    }
}
