import org.springframework.boot.gradle.plugin.SpringBootPlugin

/*
 * Copyright (c) 2019-2023 by David Gerber - https://zapek.com
 *
 * This file is part of Xeres.
 *
 * Xeres is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Xeres is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Xeres.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    ext {
        bouncycastleVersion = "1.72"
        apacheCommonsCollectionsVersion = "4.4"
        springOpenApiVersion = "2.1.0"
        jsoupVersion = "1.15.4"
        jsonPatchVersion = "1.13"
        zxingVersion = "3.5.1"
    }
}

plugins {
    id 'org.springframework.boot' version '3.0.5' apply false
    id 'org.flywaydb.flyway' version '9.5.1' apply false // Keep the version in sync with spring-boot from time to time
    id 'org.panteleyev.jpackageplugin' version '1.5.2' apply false
    id 'org.sonarqube' version '4.0.0.2929'
}

// To upgrade Gradle, change the version here, refresh, then run the 'wrapper' task
wrapper {
    gradleVersion = '8.0.2'
}

subprojects {
    group = 'io.xeres'
    version = '0.5.4'

    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(19))
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "zapek_Xeres"
        property "sonar.organization", "zapek"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.cpd.exclusion", "**/*Mapper.java"
    }
}

tasks.register('cleanProfile', Delete) {
    group = 'build'
    description = 'Deletes the user profile.'
    // when run with IDEA Ultimate
    delete 'data/userdata.mv.db'
    delete 'data/userdata.trace.db'
}
