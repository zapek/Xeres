import org.springframework.boot.gradle.plugin.SpringBootPlugin

/*
 * Copyright (c) 2019-2021 by David Gerber - https://zapek.com
 *
 * This file is part of Xeres.
 *
 * Xeres is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Xeres is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Xeres.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    ext {
        bouncycastleVersion = "1.71"
        apacheCommonsCollectionsVersion = "4.4"
        springOpenApiVersion = "1.6.11"
        jsoupVersion = "1.15.3"
        jsonPatchVersion = "1.13"
    }
}

plugins {
    id 'org.springframework.boot' version '2.7.2' apply false // 2.7.3 fails on shutdown with JavaFX launcher
    id 'org.flywaydb.flyway' version '8.5.11' apply false // Keep the version in sync with spring-boot from time to time
    id 'org.ec4j.editorconfig' version '0.0.3' apply false
    id 'org.panteleyev.jpackageplugin' version '1.4.1' apply false
    id 'com.jaredsburrows.license' version '0.9.0' apply false
    id 'org.sonarqube' version '3.4.0.2513'
}

// To upgrade Gradle, change the version here, refresh, then run the 'wrapper' task
wrapper {
    gradleVersion = '7.5.1'
}

subprojects {
    group = 'io.xeres'
    version = '0.5.3'

    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'org.ec4j.editorconfig'

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(18))
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    check.dependsOn editorconfigCheck
}

sonarqube {
    properties {
        property "sonar.projectKey", "zapek_Xeres"
        property "sonar.organization", "zapek"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.java.source", "17"
        property "sonar.cpd.exclusion", "**/*Mapper.java"
    }
}

task cleanProfile(type: Delete) {
    group = 'build'
    description = 'Deletes the user profile.'
    // when run with IDEA Ultimate
    delete 'data/userdata.mv.db'
    delete 'data/userdata.trace.db'
}
