/*
 * Copyright (c) 2019-2025 by David Gerber - https://zapek.com
 *
 * This file is part of Xeres.
 *
 * Xeres is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Xeres is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Xeres.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    ext {
        apacheCommonsCollectionsVersion = "4.5.0"
        appDirsVersion = "1.4.0"
        archunitVersion = "1.4.1"
        bouncycastleVersion = "1.81"
        commonMarkVersion = "0.24.0"
        flywayDbVersion = "11.7.2" // Only used by the plugin, keep in sync with spring-boot from time to time
        gradleVersion = "8.14.2" // To upgrade Gradle, change the version here, refresh, then run the 'build setup/wrapper' task
        jpackageVersion = "1.7.3"
        jsonPatchVersion = "1.13"
        jsoupVersion = "1.20.1"
        junitVersion = "5.13.1"
        sonarqubeVersion = "6.2.0.5505"
        springBootVersion = "3.5.2"
        springOpenApiVersion = "2.8.9"
        zxingVersion = "3.5.3"
    }
}

plugins {
    id 'org.springframework.boot' version "$springBootVersion" apply false
    id 'org.flywaydb.flyway' version "$flywayDbVersion" apply false
    id 'org.panteleyev.jpackageplugin' version "$jpackageVersion" apply false
    id 'org.sonarqube' version "$sonarqubeVersion"
}


wrapper {
    gradleVersion = "$gradleVersion"
}

// This gives a git-like version for git builds but a proper version
// when the release is built with a tag
def getVersionName = providers.exec {
    commandLine("git", "describe", "--tags")
}.standardOutput.asText.get().substring(1).trim()

subprojects {
    group = 'io.xeres'
    version = "${getVersionName}"

    apply plugin: 'java'
    apply plugin: 'jacoco'

    java {
        sourceCompatibility = '21'
    }

    compileJava {
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "zapek_Xeres"
        property "sonar.organization", "zapek"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
