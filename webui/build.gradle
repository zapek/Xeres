/*
 * Copyright (c) 2019-2021 by David Gerber - https://zapek.com
 *
 * This file is part of Xeres.
 *
 * Xeres is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Xeres is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Xeres.  If not, see <http://www.gnu.org/licenses/>.
 */

//noinspection GroovyAssignabilityCheck

plugins {
    id 'java' // it's not really java but we need it for the sourceSets
}

def webappDir = "$projectDir/src/main/webapp"
sourceSets {
    main {
        resources {
            srcDirs = ["$webappDir/dist", "$projectDir/src/main/resources"]
        }
    }
}

editorconfig {
    includes = ['src/**']
    excludes = ['src/main/webapp/dist/**', 'src/main/webapp/node_modules/**']
}

// XXX: enable once mature enough (it's slow)
//processResources {
//    dependsOn "buildAngular"
//}

tasks.register('buildAngular', Exec) {
    // installAngular should be run prior to this task
    //dependsOn "installAngular" XXX: this is slow too, so disabled
    description = 'Builds the Angular web UI.'
    workingDir "$webappDir"
    inputs.dir "$webappDir"
    // Add task to the standard build group
    group = BasePlugin.BUILD_GROUP
    // ng doesn't exist as a file in windows -> ng.cmd
    if (System.getProperty("os.name").toUpperCase(Locale.ROOT).contains("WINDOWS")) {
        commandLine "ng.cmd", "build"
    } else {
        commandLine "ng", "build"
    }
}

tasks.register('installAngular', Exec) {
    description = 'Installs everything needed for Angular development.'
    workingDir "$webappDir"
    inputs.dir "$webappDir"
    group = BasePlugin.BUILD_GROUP
    if (System.getProperty("os.name").toUpperCase(Locale.ROOT).contains("WINDOWS")) {
        commandLine "npm.cmd", "install"
    } else {
        commandLine "npm", "install"
    }
}

tasks.register('cleanAngular', Delete) {
    group = 'build'
    description = 'Deletes the Angular build directory.'
    delete "$webappDir/dist"
    delete "$webappDir/node_modules"
}
